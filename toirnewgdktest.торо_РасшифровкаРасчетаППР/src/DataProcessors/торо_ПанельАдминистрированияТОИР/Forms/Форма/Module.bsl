#Область ОбработчикиСобытийФормы

&НаСервере
Процедура тороРасшифровкаППР_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	МассивРеквизитовДляДобавления = Новый Массив;
	МассивРеквизитовДляДобавления.Добавить(Новый РеквизитФормы("тороРасшифровкаППР_СохранятьРасшифровкуППР", Новый ОписаниеТипов("Булево"),"",,Истина));
	ИзменитьРеквизиты(МассивРеквизитовДляДобавления);
	
	ЭтаФорма["тороРасшифровкаППР_СохранятьРасшифровкуППР"] = Константы.тороРасшифровкаППР_СохранятьРасшифровкуППР.Получить();
	
	ГруппаПланированиеТОИР = Элементы.Найти("ПланированиеТОИР");
	НастройкаУчетЗамещающих = Элементы.Найти("торо_УчетЗамещающихРемонтов");
	
	ГруппаСохранятьРасшифровку = Элементы.Вставить("тороРасшифровкаППР_СохранятьРасшифровкуППРГруппа", Тип("ГруппаФормы"), ГруппаПланированиеТОИР, НастройкаУчетЗамещающих);
	ГруппаСохранятьРасшифровку.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	ГруппаСохранятьРасшифровку.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСохранятьРасшифровку.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаСохранятьРасшифровку.РастягиватьПоВертикали = Ложь;
	ГруппаСохранятьРасшифровку.ОтображатьЗаголовок = Ложь;
	
	ПустаяДекорация = Элементы.Добавить("тороРасшифровкаППР_СохранятьРасшифровкуППРОтступ", Тип("ДекорацияФормы"), ГруппаСохранятьРасшифровку); 
	ПустаяДекорация.Вид = ВидДекорацииФормы.Надпись;
	ПустаяДекорация.Высота = 1;
	
	ПолеНастройки = Элементы.Добавить("тороРасшифровкаППР_СохранятьРасшифровкуППР", Тип("ПолеФормы"), ГруппаСохранятьРасшифровку);
	ПолеНастройки.Вид = ВидПоляФормы.ПолеФлажка;
	ПолеНастройки.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;	
	ПолеНастройки.ПутьКДанным = "тороРасшифровкаППР_СохранятьРасшифровкуППР";
	ПолеНастройки.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ПолеНастройки.Заголовок = НСтр("ru='Сохранять расшифровку ППР'");
	
КонецПроцедуры

&НаСервере
Процедура тороРасшифровкаППР_ПриЗаписиНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Константы.тороРасшифровкаППР_СохранятьРасшифровкуППР.Установить(ЭтаФорма["тороРасшифровкаППР_СохранятьРасшифровкуППР"]);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура тороРасшифровкаППР_торо_ИспользоватьППРПриИзмененииПосле(Элемент)
	
	Если НЕ НаборКонстант.торо_ИспользоватьППР Тогда 
		ЭтаФорма["тороРасшифровкаППР_СохранятьРасшифровкуППР"] = Ложь;
	КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
&После("УстановитьДоступностьЭлементов")
Процедура тороРасшифровкаППР_УстановитьДоступностьЭлементов()
	
	Элементы.тороРасшифровкаППР_СохранятьРасшифровкуППРГруппа.Доступность = НаборКонстант.торо_ИспользоватьППР;
	
КонецПроцедуры

#КонецОбласти
